//This example shows how to constrain a node to be on a plane defined by 3 other nodes of the model.
//The 3 nodes defining the plane can move during the simulation.
//
//Note that the plane constraint in itself only takes away 1 dof, the remaining ones must be driven apart.



Main = {

  AnyFolder MyModel = {

    
    //**************************************************************
    //Here are the 2 nodes that define the line, one of them moving.
    //Those can be any nodes in a model.
    //**************************************************************
    
    
    //***Node1, fixed.***
    AnyFixedRefFrame GlobalRef = {

      AnyDrawRefFrame Draw = {};
      
      AnyRefNode Node1 = {
        sRel = {1, 0, 1};
        AnyDrawNode Draw = { ScaleXYZ = 0.05*{1,1,1}; };
      };

    };  // Global reference frame

    
    //***Node2, moving.***
    AnySeg MovingSeg2 = 
    {
      r0 = {2, 0, 1};
      Mass = 0;
      Jii = {0, 0, 0};
      
      AnyRefNode Node2 = {
        sRel = {0, 0, 0};
        AnyDrawNode Draw = { ScaleXYZ = 0.05*{1,1,1}; };
      };
    };

    
    AnyKinEqSimpleDriver MovingSeg2Driver = 
    {
      AnyKinLinear Lin = 
      {
        AnyRefFrame &ref1 = ..MovingSeg2;
      };
      
      AnyKinRotational Rot = 
      {
        Type = RotAxesAngles;
        AnyRefFrame &ref1 = ..MovingSeg2;
      };
      DriverPos = {2, 0, 1, 0, 0, 0};
      DriverVel = {0, 0.5, -1, 0, 0, 0};
    };

    
    
    //***Node3, moving.***
    AnySeg MovingSeg3 = 
    {
      r0 = {1.2, 0, 2};
      Mass = 0;
      Jii = {0, 0, 0};
      
      AnyRefNode Node3 = {
        sRel = {0, 0, 0};
        AnyDrawNode Draw = { ScaleXYZ = 0.05*{1,1,1}; };
      };
    };

    
    AnyKinEqSimpleDriver MovingSeg3Driver = 
    {
      AnyKinLinear Lin = 
      {
        AnyRefFrame &ref1 = ..MovingSeg3;
      };
      
      AnyKinRotational Rot = 
      {
        Type = RotAxesAngles;
        AnyRefFrame &ref1 = ..MovingSeg3;
      };
      DriverPos = {1.2, 0, 2, 0, 0, 0};
      DriverVel = {0.1, 0.3, 0, 0, 0, 0};
    };
    
    
    
    
    
    //**************************************************
    //Here begins the contruction of the line constraint.
    //**************************************************
    
    
    //***This is the dummy segment that gives the direction of the line during the simulation.***
    
    AnySeg DummySeg = 
    {
      r0 = {1, 0, 1};
      Mass = 0;
      Jii = {0, 0, 0};
      
      AnyDrawRefFrame Draw = { ScaleXYZ = 0.5*{1,1,1}; RGB = {0,0,1}; };
    };
    
    
    AnySphericalJoint DummySegJoint = 
    {
      AnyRefFrame &ref1 = .GlobalRef.Node1;
      AnyRefFrame &ref2 = .DummySeg;
    };
    
    
    AnyKinEqSimpleDriver DunnySegDriver = 
    {
      MeasureOrganizer = {1,2,4};
      AnyKinLinear Lin1 = 
      {
        Ref = 0;
        AnyRefFrame &ref1 = ..DummySeg;
        AnyRefFrame &ref2 = ..MovingSeg2.Node2;
      };
      
      AnyKinLinear Lin2 = 
      {
        Ref = 0;
        AnyRefFrame &ref1 = ..DummySeg;
        AnyRefFrame &ref2 = ..MovingSeg3.Node3;
      };
      
      DriverPos = {0, 0 ,0};
      DriverVel = {0, 0, 0};
    };
    
    

    
    //***This is the segment that is constrained to be on the line.***
    AnySeg SegOnLine = 
    {
      r0 = {1.5, 0, 1.5};
      Mass = 0;
      Jii = {0, 0, 0};
      
      AnyRefNode Node = {
        sRel = {0, 0, 0};
        AnyDrawNode Draw = { ScaleXYZ = 0.05*{1,1,1}; RGB = {0,1,0}; };
      };
    };
    
    
    //***This is the line constraint, it only takes away 1 dof.***
    AnyKinEqSimpleDriver SegOnLineDriver1 = 
    {
      MeasureOrganizer = {1};
      AnyKinLinear Lin = 
      {
        Ref = 0;
        AnyRefFrame &ref1 = ..DummySeg;
        AnyRefFrame &ref2 = ..SegOnLine.Node;
      };
      DriverPos = {0};
      DriverVel = {0};
    };
    
    
    //***The 5 remaining dof must be driven as well, this could be any kind of driver.***
    AnyKinEqSimpleDriver SegOnLineDriver2 = 
    {
      MeasureOrganizer = {0,2,3,4,5};
      AnyKinLinear Lin = 
      {
        AnyRefFrame &ref1 = ..SegOnLine.Node;
      };
      
      AnyKinRotational Rot = 
      {
        Type = RotAxesAngles;
        AnyRefFrame &ref1 = ..SegOnLine.Node;
      };
      DriverPos = {1.5, 1.5, 0, 0, 0};
      DriverVel = {0, 0, 0, 0, 0};
    };
    
    
    

  }; // MyModel

  
  
  
  
  // The study: Operations to be performed on the model
  AnyBodyStudy MyStudy = {
    AnyFolder &Model = .MyModel;
    Gravity = {0.0, -9.81, 0.0};
  };

};  // Main
